# $Id: PKGBUILD 169467 2012-10-21 18:14:40Z dreisner $
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=syslog-ng
pkgver=3.3.6
_arch_rel=3
_axe_rel=1

pkgrel="${_AXE_PREFIX_REL}.${_AXE_STAGE}.${_arch_rel}.${_axe_rel}.${_AXE_PROCESOR_REL}.${_AXE_SUFFIX_REL}"
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('axis')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('awk' 'eventlog' 'glib2' 'libcap' 'openssl' 'udev')
makedepends=('flex' 'pkg-config')
optdepends=('logrotate: for rotating log files')
options=('!libtool')
backup=('etc/syslog-ng/modules.conf'
        'etc/syslog-ng/scl.conf'
        'etc/syslog-ng/syslog-ng.conf'
        'etc/conf.d/syslog-ng'
        'etc/logrotate.d/syslog-ng')
install=$pkgname.install
source=("http://www.balabit.com/downloads/files/syslog-ng/sources/$pkgver/source/${pkgname}_$pkgver.tar.gz"
        syslog-ng.conf
        syslog-ng.conf.d
        syslog-ng.logrotate
        syslog-ng.rc
        0001-afsocket-Fix-compilation-without-spoof-source.patch)
sha1sums=('c2ee74180d640b235665115c5a4b2c61a71a9576'
          'db14f4937a821e449b769c938faf2275a7ef188e'
          '9b2eb6ea9e27c9f1b6c1c855be211ec3da51d3c8'
          '949128fe3d7f77a7aab99048061f885bc758000c'
          '3db145595628cf31958fc2b9a904d57b638b4f11'
          '5f0e6a4b01ba59724bba8932c41ac8f70386659e')

build() {
  cd "$pkgname-$pkgver"

  # upstream commit 21a9ac3a7e2554f94b8ff8788bc4e82ec3fb58d3
  patch -Np1 <"$srcdir"/0001-afsocket-Fix-compilation-without-spoof-source.patch

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/syslog-ng \
    --libexecdir=/usr/lib \
    --localstatedir=/var/lib/syslog-ng \
    --datadir=/usr/share/syslog-ng \
    --with-pidfile-dir=/run \
    --disable-spoof-source \
    --disable-systemd \

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/var/lib/syslog-ng" "$pkgdir/etc/syslog-ng/patterndb.d"
  install -Dm644 "$srcdir/syslog-ng.conf" "$pkgdir/etc/syslog-ng/syslog-ng.conf"
  install -Dm644 "$srcdir/syslog-ng.logrotate" "$pkgdir/etc/logrotate.d/syslog-ng"
  install -Dm755 "$srcdir/syslog-ng.rc" "$pkgdir/etc/rc.d/syslog-ng"
  install -Dm644 "$srcdir/syslog-ng.conf.d" "$pkgdir/etc/conf.d/syslog-ng"
}
#----------------axe warning------------------ /root/axebuilder/AxePkgbuildsUpdatable/syslog-ng/syslog-ng.rc altered for rc.d changes
#----------------axe warning------------------ syetemd detected
