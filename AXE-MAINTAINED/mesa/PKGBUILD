# $Id$
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa
pkgname=('libglapi' 'libgl' 'mesa' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel' 'ati-dri' 'intel-dri' 'svga-dri' 'nouveau-dri')

#_git=true
#_gitdate=20121006
_git=false
pkgver=9.0.1
_axe_pkgrel=1
pkgrel=991.${_axe_pkgrel}.1axe
arch=('i686' 'x86_64')
makedepends=('glproto>=1.4.16' 'libdrm>=2.4.39' 'libxxf86vm>=1.1.2' 'libxdamage>=1.1.3' 'expat>=2.1.0' 'libx11>=1.5.0' 'libxt>=1.1.3'
             'gcc-libs>=4.7.1-6' 'dri2proto>=2.8' 'python2' 'libxml2' 'imake' 'llvm' 'udev' 'libvdpau>=0.5')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=('LICENSE'
		"ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2")

md5sums=('5c65a0fe315dd347e09b1f2826a1df5a'
         '97d6554c05ea7449398afe3a0ede7018')

build() {
    cd ${srcdir}/Mesa-*

    COMMONOPTS="--prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gallium-egl \
    --with-egl-platforms=x11,drm \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau "

# not default:
#    --enable-gallium-egl enable optional EGL state tracker (not required for
#                          EGL support in Gallium with OpenGL and OpenGL ES)
#                          [default=disable]
#    --enable-xa             enable build of the XA X Acceleration API                          [default=no]


    autoreconf -vfi
    ./configure \
      $COMMONOPTS

    make
}

package_libglapi() {
  depends=('glibc')
  pkgdesc="free implementation of the GL API -- shared library. The Mesa GL API module is responsible for dispatching all the gl* functions"
  groups=('axis')

  make -C ${srcdir}/Mesa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/libglapi"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libglapi/"
}

package_libgl() {
  depends=('libdrm>=2.4.39' 'libxxf86vm>=1.1.2' 'libxdamage>=1.1.3' 'expat>=2.1.0' 'libglapi' 'gcc-libs')
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"
  groups=('axis')

  # fix linking because of splitted package
  make -C ${srcdir}/Mesa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install

  # libGL & libdricore
  make -C ${srcdir}/Mesa-*/src/glx DESTDIR="${pkgdir}" install
  make -C ${srcdir}/Mesa-*/src/mesa/libdricore DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/Mesa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" uninstall


  make -C ${srcdir}/Mesa-*/src/gallium/targets/dri-swrast DESTDIR="${pkgdir}" install

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  install -m755 -d "${pkgdir}/usr/share/licenses/libgl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgl/"
}

package_mesa() {
  # check also gl.pc
  depends=('libgl' 'libx11>=1.5.0' 'libxext>=1.3.1' 'libxdamage' 'libxfixes' 'libxcb' 'libxxf86vm')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  pkgdesc="Mesa 3-D graphics libraries and include files"
  groups=('axis')

  make -C ${srcdir}/Mesa-*/src/mesa DESTDIR="${pkgdir}" install-glHEADERS
  make -C ${srcdir}/Mesa-*/src/mesa/drivers/dri DESTDIR="${pkgdir}" install-driincludeHEADERS
  make -C ${srcdir}/Mesa-*/src/mesa DESTDIR="${pkgdir}" install-pkgconfigDATA
  make -C ${srcdir}/Mesa-*/src/mesa/drivers/dri DESTDIR="${pkgdir}" install-pkgconfigDATA
  make -C ${srcdir}/Mesa-*/src/mesa/drivers/dri/common DESTDIR="${pkgdir}" install-sysconfDATA

  make -C ${srcdir}/Mesa-*/src/gallium/targets/xa-vmwgfx DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"
}

package_osmesa() {
  depends=('libglapi' 'gcc-libs')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  pkgdesc="Mesa 3D off-screen rendering library"
  groups=('axis')

  # fix linking because of splitted package
  make -C ${srcdir}/Mesa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install

  make -C ${srcdir}/Mesa-*/src/mesa/drivers/osmesa DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/Mesa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/osmesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/osmesa/"
}

package_libgbm() {
  depends=('udev' 'libglapi' 'libdrm')
  pkgdesc="Mesa gbm library"
  groups=('axis')

  # fix linking because of splitted package
  make -C ${srcdir}/Mesa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install

  make -C ${srcdir}/Mesa-*/src/gbm DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/Mesa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/libgbm"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgbm/"
}

package_libgles() {
  depends=('libglapi' 'libdrm' 'khrplatform-devel')
  pkgdesc="Mesa GLES libraries and headers"
  groups=('axis')

  # fix linking because of splitted package
  make -C ${srcdir}/Mesa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install

  make -C ${srcdir}/Mesa-*/src/mapi/es1api DESTDIR="${pkgdir}" install
  make -C ${srcdir}/Mesa-*/src/mapi/es2api DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/Mesa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/libgles"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgles/"
}

package_libegl() {
  # check also egl.pc
  depends=('libx11' 'libxext' 'libxdamage' 'libxfixes' 'libxxf86vm' 'libxcb' 'libgbm' 'khrplatform-devel')
  pkgdesc="Mesa EGL libraries and headers"
  groups=('axis')

  make -C ${srcdir}/Mesa-*/src/gallium/targets/egl-static DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/doc/libegl"
  install -m644 ${srcdir}/Mesa-*/docs/egl.html "${pkgdir}/usr/share/doc/libegl/"

  # fix linking because of splitted package
  make -C ${srcdir}/Mesa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install
  make -C ${srcdir}/Mesa-*/src/gbm DESTDIR="${pkgdir}" install

  make -C ${srcdir}/Mesa-*/src/egl DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/Mesa-*/src/gbm DESTDIR="${pkgdir}" uninstall
  make -C ${srcdir}/Mesa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/libegl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libegl/"

  # fix file conflicts
  rm -rf ${pkgdir}/usr/include/KHR
}

package_khrplatform-devel() {
  pkgdesc="Khronos platform development package"
  groups=('axis')

  install -m755 -d "${pkgdir}/usr/include/KHR"
  install -m644 ${srcdir}/Mesa-*/include/KHR/khrplatform.h "${pkgdir}/usr/include/KHR/"

  install -m755 -d "${pkgdir}/usr/share/licenses/khrplatform-devel"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/khrplatform-devel/"
}

package_ati-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI radeon/r200 + Gallium3D r300,r600 drivers for AMD/ATI Radeon"
  conflicts=('xf86-video-ati<6.9.0-6')
  groups=('axis')

  # fix linking because of splitted package
  make -C ${srcdir}/Mesa-*/src/mesa/libdricore DESTDIR="${pkgdir}" install

  # classic mesa drivers for radeon,r200
  make -C ${srcdir}/Mesa-*/src/mesa/drivers/dri/radeon DESTDIR="${pkgdir}" install
  make -C ${srcdir}/Mesa-*/src/mesa/drivers/dri/r200 DESTDIR="${pkgdir}" install
  # gallium3D driver for r300,r600,radeonsi
  make -C ${srcdir}/Mesa-*/src/gallium/targets/dri-r300 DESTDIR="${pkgdir}" install
  make -C ${srcdir}/Mesa-*/src/gallium/targets/dri-r600 DESTDIR="${pkgdir}" install
  make -C ${srcdir}/Mesa-*/src/gallium/targets/dri-radeonsi DESTDIR="${pkgdir}" install
  # vdpau driver
  make -C ${srcdir}/Mesa-*/src/gallium/targets/vdpau-r300 DESTDIR="${pkgdir}" install
  make -C ${srcdir}/Mesa-*/src/gallium/targets/vdpau-r600 DESTDIR="${pkgdir}" install
  make -C ${srcdir}/Mesa-*/src/gallium/targets/vdpau-radeonsi DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/Mesa-*/src/mesa/libdricore DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/ati-dri/"
}

package_intel-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for Intel"
  groups=('axis')

  # fix linking because of splitted package
  make -C ${srcdir}/Mesa-*/src/mesa/libdricore DESTDIR="${pkgdir}" install

  make -C ${srcdir}/Mesa-*/src/mesa/drivers/dri/i915 DESTDIR="${pkgdir}" install
  make -C ${srcdir}/Mesa-*/src/mesa/drivers/dri/i965 DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/Mesa-*/src/mesa/libdricore DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/intel-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/intel-dri/"
}

package_nouveau-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa classic DRI + Gallium3D drivers for Nouveau"
  groups=('axis')

  # fix linking because of splitted package
  make -C ${srcdir}/Mesa-*/src/mesa/libdricore DESTDIR="${pkgdir}" install

  # classic mesa driver for nv10 , nv20 nouveau_vieux_dri.so
  make -C ${srcdir}/Mesa-*/src/mesa/drivers/dri/nouveau DESTDIR="${pkgdir}" install
  # gallium3D driver for nv30 - nv40 - nv50 nouveau_dri.so
  make -C ${srcdir}/Mesa-*/src/gallium/targets/dri-nouveau DESTDIR="${pkgdir}" install
  # vdpau driver
  make -C ${srcdir}/Mesa-*/src/gallium/targets/vdpau-nouveau DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/Mesa-*/src/mesa/libdricore DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/nouveau-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/nouveau-dri/"
}

package_svga-dri() {
  depends=('gcc-libs' 'libdrm' 'expat')
  pkgdesc="Gallium3D VMware guest GL driver"
  groups=('axis')

  make -C ${srcdir}/Mesa-*/src/gallium/targets/dri-vmwgfx DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/svga-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/svga-dri/"
}
