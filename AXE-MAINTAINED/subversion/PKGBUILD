# $Id: PKGBUILD 170250 2012-11-06 11:27:11Z stephane $
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

#--------------------
pkgname=subversion
#--------------------
pkgver=1.7.8
_arch_rel=1
_axe_rel=1
#--------------------
pkgrel="${_AXE_PREFIX_REL}.${_AXE_STAGE}.${_arch_rel}.${_axe_rel}.${_AXE_PROCESOR_REL}.${_AXE_SUFFIX_REL}"
pkgdesc="A Modern Concurrent Version Control System"
arch=('i686' 'x86_64')
license=('APACHE')
groups=('axis')
depends=('neon' 'apr-util' 'sqlite' 'file' 'serf')
optdepends=('bash-completion: for svn bash completion' 'python2: for some hook scripts' 'java-environment') #'ruby: for some hook scripts')
makedepends=('krb5' 'apache' 'python2' 'perl' 'swig' 'java-runtime' 'java-environment'
             'autoconf' 'db' 'e2fsprogs')
backup=('etc/xinetd.d/svn' 'etc/conf.d/svnserve')
url="http://subversion.apache.org/"
provides=('svn')
options=('!makeflags' '!libtool' '!emptydirs')
install=${pkgname}.install
source=(http://apache.mirror.rafal.ca/subversion/$pkgname-$pkgver.tar.bz2{,.asc}
        svnserve
        svn
        svnserve.conf
        svnserve.tmpfiles
        svnserve.service
        subversion-kwallet.patch        
        subversion.rpath.fix.patch)
sha1sums=('12c7d8d5414bba74c9777c4d1dae74f152df63c2'
          '24f8e82fa131c1a1b924d71046f8d38cb00645a7'
          'dd5f6957f2095eec97d4f8609fd9c344a1c135b5'
          '73b36c046c09cec2093354911c89e3ba8056af6c'
          'ad117bf3b2a838a9a678a93fd8db1a066ad46c41'
          '00cc36e69077a8c45674ead6dd62a7647b3a3b6d'
          '9f6790d842cf3e0228b007483a43e39a42724068'
          'f2aed28a2e34b8631ee9b69175bac1bac59327ce'
          '3d1e28408a9abb42af2e531adc0d01ce21acfad6')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   export PYTHON=/usr/bin/python2

   patch -Np0 -i ../subversion.rpath.fix.patch
   sed -i 's|/usr/bin/env python|/usr/bin/env python2|' tools/hook-scripts/{,mailer/{,tests/}}*.py
#axe rel 1 .. we are not using kde
#   patch -Np0 -i ../subversion-kwallet.patch

   ./configure --prefix=/usr \
				--with-apr=/usr \
				--with-apr-util=/usr \
               --with-zlib=/usr \
			   --with-neon=/usr \
			   --with-apxs \
               --with-sqlite=/usr \
			   --with-berkeley-db=:/usr/include/:/usr/lib:db-5.3 \
               --enable-javahl \
			   --without-gnome-keyring \
			   --without-kwallet \
			   --disable-experimental-libtool \
				--without-jikes \
				--enable-local-library-preloading \
				--disable-mod-activation \
				--disable-neon-version-check \
				--disable-static
			   

   make external-all
   make LT_LDFLAGS="-L$Fdestdir/usr/lib" local-all
   make swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
     swig_pydir_extra=/usr/lib/python2.7/site-packages/svn swig-py swig-pl javahl #swig-rb
}

#check() {
#   cd "${srcdir}/${pkgname}-${pkgver}"
#   export LANG=C LC_ALL=C
#   make check check-swig-pl check-swig-py check-javahl CLEANUP=yes # check-swig-rb
#}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   export LD_LIBRARY_PATH="${pkgdir}"/usr/lib:${LD_LIBRARY_PATH}
   make DESTDIR="${pkgdir}" INSTALLDIRS=vendor \
     swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
     swig_pydir_extra=/usr/lib/python2.7/site-packages/svn \
     install install-swig-py install-swig-pl install-javahl #install-swig-rb

   install -dm755 "${pkgdir}"/usr/share/subversion
   cp -a tools/hook-scripts "${pkgdir}"/usr/share/subversion/
   rm "${pkgdir}"/usr/share/subversion/hook-scripts/*.in

   rm "${pkgdir}"/usr/lib/perl5/vendor_perl/auto/SVN/_Core/.packlist
   rm -r "${pkgdir}"/usr/lib/perl5/core_perl

   ## svnserve ...

   # ... iniscript/xinetd
   install -D -m 755 "${srcdir}"/svnserve "${pkgdir}"/etc/rc.d/svnserve
   install -D -m 644 "${srcdir}"/svn "${pkgdir}"/etc/xinetd.d/svn

   # ... systemd
#   install -D -m 644 "${srcdir}"/svnserve.service  "${pkgdir}"/usr/lib/systemd/system/svnserve.service
#   install -D -m 644 "${srcdir}"/svnserve.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/svnserve.conf

   # ... common config
   install -D -m 644 "${srcdir}"/svnserve.conf "${pkgdir}"/etc/conf.d/svnserve

   install -Dm 644 tools/client-side/bash_completion \
     "${pkgdir}"/usr/share/bash-completion/completions/subversion
   for i in svn svnadmin svndumpfilter svnlook svnsync svnversion; do
      ln -sf subversion "${pkgdir}"/usr/share/bash-completion/completions/${i}
   done
}
#----------------axe warning------------------ /root/axebuilder/AxePkgbuildsUpdatable/subversion/svnserve altered for rc.d changes
#----------------axe warning------------------ syetemd detected
