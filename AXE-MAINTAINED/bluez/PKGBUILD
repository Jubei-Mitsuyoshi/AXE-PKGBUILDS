# $Id: PKGBUILD 162308 2012-06-24 20:46:50Z tomegun $
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
#axe-progress- lots of patches from gentoo to check and put in - URGENT !!!

pkgname=bluez
pkgver=4.101
pkgrel=99.1axe
pkgdesc="Libraries and tools for the Bluetooth protocol stack  --maintained as part of the axe project"
url="http://www.bluez.org/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('dbus-core' 'python2' 'udev')
makedepends=('gstreamer0.10-base' 'libusb-compat' 'libsndfile' 'libusbx' 'flex')
optdepends=("gstreamer0.10-base: bluetooth GStreamer support"
            "alsa-lib: Audio bluetooth devices support"
            "dbus-python: to run bluez-simple-agent"
            "pygobject: to run bluez-simple-agent"
            "libusb-compat: USB adapters support"
            "cups: CUPS backend")
conflicts=('bluez-libs' 'bluez-utils')
provides=('bluez-libs' 'bluez-utils')
replaces=('bluez-libs' 'bluez-utils')
groups=('axis')
options=('!libtool')
backup=(etc/bluetooth/{main,rfcomm,audio,network,input,serial}.conf
        'etc/conf.d/bluetooth')
source=("http://www.kernel.org/pub/linux/bluetooth/${pkgname}-${pkgver}.tar.bz2"
        'bluetooth.conf.d'
	'rc.bluetooth')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/lib \
    --enable-gstreamer \
    --enable-alsa \
    --enable-usb \
    --enable-tools \
    --enable-bccmd \
    --enable-dfutool \
    --enable-hid2hci \
    --enable-hidd \
    --enable-pand \
    --enable-dund \
    --enable-cups \
    --enable-wiimote \
    --disable-test
 

  make
}

#masked  out by axe,AXE does not like systemd
 #  --with-systemdunitdir=/usr/lib/systemd/system


package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm755 ${srcdir}/rc.bluetooth ${pkgdir}/etc/rc.d/bluetooth
  
  install -d ${pkgdir}/etc/bluetooth
  install -m644 network/network.conf \
                input/input.conf \
                audio/audio.conf \
                serial/serial.conf \
    ${pkgdir}/etc/bluetooth/
  
  install -Dm644 ${srcdir}/bluetooth.conf.d \
    ${pkgdir}/etc/conf.d/bluetooth

  # FS#27630
  install -Dm755 test/simple-agent "${pkgdir}"/usr/bin/bluez-simple-agent
  install -Dm755 test/test-device "${pkgdir}"/usr/bin/bluez-test-device
  install -Dm755 test/test-input "${pkgdir}"/usr/bin/bluez-test-input
  sed -i 's#/usr/bin/python#/usr/bin/python2#' \
    "${pkgdir}"/usr/bin/bluez-simple-agent \
    "${pkgdir}"/usr/bin/bluez-test-device \
    "${pkgdir}"/usr/bin/bluez-test-input
}
md5sums=('902b390af95c6c5d6d1a17d94c8344ab'
         '7412982b440f29fa7f76a41a87fef985'
         '864cbd24e6efc3592e9284b0b5fb2cfd')
