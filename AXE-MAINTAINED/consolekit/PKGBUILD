# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Ivailo Monev <xakepa10@gmail.com>


#--------------------
pkgname=consolekit
#--------------------
pkgver=0.4.6
_arch_rel=1
_axe_native_rel=1
#--------------------
pkgrel="${_AXE_PREFIX_REL}.${_AXE_STAGE}.${_arch_rel}.${_axe_native_rel}.${_AXE_PROCESOR_REL}.${_AXE_SUFFIX_REL}"
pkgdesc="A framework for defining and tracking users, login sessions, and seats,"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/ConsoleKit"
license=('GPL')
groups=('axis')
depends=('polkit' 'zlib' 'libx11' 'dbus-glib')
makedepends=('pkgconfig' 'xmlto' 'docbook-xsl')
options=(!libtool)
#source=(http://www.freedesktop.org/software/ConsoleKit/dist/ConsoleKit-$pkgver.tar.bz2
# this has not been released yet, so this is a snapshot
source=("ftp://ftp.archlinux.org/other/${pkgname}/ConsoleKit-${pkgver}.tar.bz2"
        'consolekit.logrotate' 'consolekit.tmpfiles.conf')
md5sums=('810990b607e338f06a6396c92b8a4a06'
         '6fefa451d9fe2fc6d6269629d3529793'
         '8c5a8059db515d40fae1379daf084094')

build() {
  cd "$srcdir/ConsoleKit-$pkgver"
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/ConsoleKit \
      --enable-docbook-docs \
      --enable-udev-acl
  make
}

package() {
  cd "$srcdir/ConsoleKit-$pkgver"
  make DESTDIR="$pkgdir" install

  # install the logrotate config
  install -D -m644 "$srcdir/consolekit.logrotate" "$pkgdir/etc/logrotate.d/consolekit"

  install -D -m644 "$srcdir/consolekit.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/consolekit.conf"

  rm -rf "${pkgdir}/var/run"
}
