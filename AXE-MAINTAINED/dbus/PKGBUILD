# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
pkgname=dbus
pkgdesc="Freedesktop.org message bus system, --maintained as part of the axe project"
pkgver=1.6.8
_axe_pkgrel=1
pkgrel=991.${_axe_pkgrel}.2axe
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
groups=('axis')
depends=("dbus-core>=$pkgver" 'libx11')
source=(http://dbus.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz #{,.asc}
        30-dbus)
md5sums=('3bf059c7dd5eda5f539a1b7cfe7a14a2'
         '9fafe8b28460aeaa6054309ef4c5ed92')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 \
      --with-dbus-user=81 \
      --with-system-pid-file=/var/run/dbus.pid \
      --without-systemdsystemunitdir \
      --enable-inotify \
      --disable-verbose-mode \
      --disable-static \
      --disable-dnotify \
      --disable-tests \
      --disable-asserts \
	  --disable-systemd
      
  make -C tools dbus-launch
}

package() {
  cd $pkgname-$pkgver
  install -m755 -d "$pkgdir/usr/bin"
  install -m755 -d "$pkgdir/usr/share/man/man1"
  install -m755 tools/dbus-launch "$pkgdir/usr/bin/"
  install -m644 doc/dbus-launch.1 "$pkgdir/usr/share/man/man1/"

  install -m755 -d "$pkgdir/etc/X11/xinit/xinitrc.d"
  install -m755 ../30-dbus "$pkgdir/etc/X11/xinit/xinitrc.d/"

  install -d -m755 "$pkgdir/usr/share/licenses/dbus"
  install -m644 COPYING "$pkgdir/usr/share/licenses/dbus/"
}
