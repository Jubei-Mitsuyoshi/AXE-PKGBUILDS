# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
pkgname=dbus-core
pkgdesc="Freedesktop.org message bus system, --maintained as part of the axe project"
pkgver=1.6.8
_arch_rel=3
_axe_rel=2

pkgrel="${_AXE_PREFIX_REL}.${_AXE_STAGE}.${_arch_rel}.${_axe_rel}.${_AXE_PROCESOR_REL}.${_AXE_SUFFIX_REL}"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
groups=('axis')
depends=('expat' 'coreutils' 'filesystem' 'udev' 'shadow') # shadow for install scriptlet FS#29341
makedepends=('libx11')
options=(!libtool)
install=dbus.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz
        dbus.rc)
md5sums=('3bf059c7dd5eda5f539a1b7cfe7a14a2'
         '3d1670495352e913d50f48c8d60d55ef')

build() {
  cd dbus-$pkgver
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
	  --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 \
	  --with-dbus-user=81 \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --with-session-socket-dir=/tmp \
      --with-xml=expat \
      --with-x \
      --enable-inotify \
	  --disable-dnotify \
      --disable-verbose-mode \
	  --disable-static \
      --disable-tests \
	  --disable-asserts \
	  --disable-systemd
      
  make
}

package(){
  cd dbus-$pkgver
  make DESTDIR="$pkgdir" install

  rm -f "$pkgdir/usr/bin/dbus-launch"
  rm -f "$pkgdir/usr/share/man/man1/dbus-launch.1"
  rm -rf "$pkgdir/var/run"

  install -m755 -d "$pkgdir/etc/rc.d"
  install -m755 ../dbus.rc "$pkgdir/etc/rc.d/dbus"

  install -dm755 "$pkgdir/usr/share/licenses/dbus-core"
  install -m644 COPYING "$pkgdir/usr/share/licenses/dbus-core/"
}
