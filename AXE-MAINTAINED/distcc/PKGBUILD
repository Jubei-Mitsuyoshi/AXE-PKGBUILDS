# $Id: PKGBUILD 75874 2012-09-04 15:27:48Z spupykin $
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=distcc
pkgver=3.1
_arch_rel=8
_axe_rel=1

pkgrel="${_AXE_EXPORTED_PREFIX_REL}.${_AXE_EXPORTED_STAGE}.${_arch_rel}.${_axe_rel}.${_AXE_EXPORTED_PROCESSOR_TYPE}.${_AXE_EXPORTED_SUFFIX_REL}"
pkgdesc="A distributed C, C++, Obj C compiler  --maintained as part of the axe project"
arch=('i686' 'x86_64')
url="http://code.google.com/p/distcc/"
license=('GPL')
groups=('axis')
depends=('gcc' 'popt' 'avahi')
makedepends=('gtk2' 'pkgconfig' 'python2')
optdepends=('gtk2: for distccmon-gnome'
	    'python2')
backup=(etc/conf.d/distccd)
source=(http://distcc.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2
	distccd
	distccd.conf.d
	distccd.service)
md5sums=('a1a9d3853df7133669fffec2a9aab9f3'
         '1c918474372c09304772c20c4de484fa'
         '89aaf6e9072092e283465a14e83f0f94'
         'bb24748257d9b0739e18b494cae08633')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [ -f Makefile ] || PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
	--with-gtk \
	--sysconfdir=/etc \
	--mandir=/usr/share/man
#	--without-avahi \

  make WERROR_CFLAGS= INCLUDESERVER_PYTHON=/usr/bin/python2
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
#  make TEST_PYTHON=/usr/bin/python2 check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" INCLUDESERVER_PYTHON=/usr/bin/python2 install
  install -D -m644 ${srcdir}/distccd.conf.d ${pkgdir}/etc/conf.d/distccd
  install -D -m755 ${srcdir}/distccd ${pkgdir}/etc/rc.d/distccd

  install -d ${pkgdir}/usr/lib/${pkgname}/bin
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/cc
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/gcc
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/g++
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/c++
  ln -sf /usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/bin/cpp
 #masked  out by axe,AXE does not like systemd
#  install -Dm0644 $srcdir/distccd.service $pkgdir/usr/lib/systemd/system/distccd.service
}
