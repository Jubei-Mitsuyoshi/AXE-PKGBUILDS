# $Id: PKGBUILD 171552 2012-11-19 03:55:36Z bisson $
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=openssh
pkgver=6.1p1
_axe_pkgrel=1
pkgrel=991.${_axe_pkgrel}.4axe
pkgdesc='Free version of the SSH connectivity tools'
url='http://www.openssh.org/portable.html'
license=('custom:BSD')
groups=("axis")
arch=('i686' 'x86_64')
makedepends=('linux-headers')
depends=('krb5' 'openssl' 'libedit' 'ldns')
optdepends=('xorg-xauth: X11 forwarding'
            'x11-ssh-askpass: input passphrase in X')
source=("ftp://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/${pkgname}-${pkgver}.tar.gz"
        'sshd.close-sessions'
        'sshdgenkeys.service'
        'sshd@.service'
        'sshd.service'
        'sshd.socket'
        'sshd.confd'
        'sshd.pam'
        'sshd')
sha1sums=('751c92c912310c3aa9cadc113e14458f843fc7b3'
          'b97e513407d2d955f73e1884c4dc19142e292175'
          '6df5be396f8c593bb511a249a1453294d18a01a6'
          'bd6eae36c7ef9efb7147778baad7858b81f2d660'
          'f9af4a442b804ab661cec0edb25dd76dee16d8d2'
          'e12fa910b26a5634e5a6ac39ce1399a132cf6796'
          'ec102deb69cad7d14f406289d2fc11fee6eddbdd'
          '659e3ee95c269014783ff8b318c6f50bf7496fbd'
          '378b35f7773c17c03d5a2f364c2a4faac21355cc')

backup=('etc/ssh/ssh_config' 'etc/ssh/sshd_config' 'etc/pam.d/sshd' 'etc/conf.d/sshd')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/ssh \
		--sysconfdir=/etc/ssh \
		--with-ldns \
		--with-libedit \
		--with-ssl-engine \
		--with-pam \
		--with-privsep-user=nobody \
		--with-kerberos5=/usr \
		--with-xauth=/usr/bin/xauth \
		--with-mantype=man \
		--with-md5-passwords \
		--with-pid-dir=/run \

	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make tests ||
	grep $USER /etc/passwd | grep -q /bin/false
	# connect.sh fails when run with stupid login shell
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/share/man/man1/slogin.1
	ln -sf ssh.1.gz "${pkgdir}"/usr/share/man/man1/slogin.1.gz

	install -Dm644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"

#	install -Dm644 ../sshdgenkeys.service "${pkgdir}"/usr/lib/systemd/system/sshdgenkeys.service
#	install -Dm644 ../sshd@.service "${pkgdir}"/usr/lib/systemd/system/sshd@.service
#	install -Dm644 ../sshd.service "${pkgdir}"/usr/lib/systemd/system/sshd.service
#	install -Dm644 ../sshd.socket "${pkgdir}"/usr/lib/systemd/system/sshd.socket

	install -Dm755 ../sshd.close-sessions "${pkgdir}/etc/rc.d/functions.d/sshd-close-sessions" # FS#17389
	install -Dm644 ../sshd.confd "${pkgdir}"/etc/conf.d/sshd
	install -Dm644 ../sshd.pam "${pkgdir}"/etc/pam.d/sshd
	install -Dm755 ../sshd "${pkgdir}"/etc/rc.d/sshd

	install -Dm755 contrib/findssl.sh "${pkgdir}"/usr/bin/findssl.sh
	install -Dm755 contrib/ssh-copy-id "${pkgdir}"/usr/bin/ssh-copy-id
	install -Dm644 contrib/ssh-copy-id.1 "${pkgdir}"/usr/share/man/man1/ssh-copy-id.1

	sed \
		-e '/^#ChallengeResponseAuthentication yes$/c ChallengeResponseAuthentication no' \
		-e '/^#UsePAM no$/c UsePAM yes' \
		-i "${pkgdir}"/etc/ssh/sshd_config
}
#----------------axe warning------------------ syetemd detected in file /root/axebuilder/AxePkgbuildsUpdatable/openssh/PKGBUILD 
#----------------axe warning------------------ /root/axebuilder/AxePkgbuildsUpdatable/openssh/sshd altered for rc.d changes
#----------------axe warning------------------ /root/axebuilder/AxePkgbuildsUpdatable/openssh/sshd.close-sessions altered for rc.d changes
#--------aex hand note, need to understand if we need to remove that reference below
#----------------axe warning------------------ syetemd detected in file /root/axebuilder/AxePkgbuildsUpdatable/openssh/sshd.pam 
