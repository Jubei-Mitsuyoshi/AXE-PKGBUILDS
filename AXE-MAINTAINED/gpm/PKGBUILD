# $Id: PKGBUILD 163618 2012-07-17 05:32:49Z eric $
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>

pkgname=gpm
pkgver=1.20.7
_arch_rel=1
_axe_rel=1

pkgrel="${_AXE_PREFIX_REL}.${_AXE_STAGE}.${_arch_rel}.${_axe_rel}.${_AXE_PROCESOR_REL}.${_AXE_SUFFIX_REL}"
pkgdesc="A mouse server for the console and xterm --maintained as part of the axe project"
arch=('i686' 'x86_64')
url="http://www.nico.schottelius.org/software/gpm/"
license=('GPL')
groups=('axis')
depends=('ncurses' 'bash')
backup=('etc/conf.d/gpm')
options=('!makeflags')
install=gpm.install
source=(http://www.nico.schottelius.org/software/gpm/archives/${pkgname}-${pkgver}.tar.lzma \
        gpm gpm.conf.d gpm.sh gpm.service)
sha1sums=('8d9f3655c80ce7667d31ede2a100b44986480369'
          '19e1feb1493373512a77801699df012d186336ea'
          '4c31cb7dd51cee4d16d3f7a8956e6d87fac1ad86'
          '88fe5ff10916c68a87abc8418a56eb0073f69fa9'
          '20b92360f0ad38a2032fcae37bdbd01b31e43f77')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m755 ../gpm "${pkgdir}/etc/rc.d/gpm"
  install -D -m644 ../gpm.conf.d "${pkgdir}/etc/conf.d/gpm"
  install -D -m755 ../gpm.sh "${pkgdir}/etc/profile.d/gpm.sh"
  	# commented out by AXE, axe does not like systemd
 # install -D -m644 ../gpm.service "${pkgdir}/usr/lib/systemd/system/gpm.service"

# library fixes
  cd "${pkgdir}/usr/lib/"
  ln -s libgpm.so.2.* libgpm.so
  chmod 755 "${pkgdir}"/usr/lib/libgpm.so.*
}
