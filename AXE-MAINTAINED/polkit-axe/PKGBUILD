# $Id: PKGBUILD 169989 2012-10-30 23:00:46Z heftig $
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=polkit-axe
_axepkgname=polkit
pkgver=0.107
_axe_pkgrel=1
pkgrel=991.${_axe_pkgrel}.4axe
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(i686 x86_64)
license=('LGPL')
groups=("axis")
url="http://www.freedesktop.org/wiki/Software/PolicyKit"
depends=('glib2' 'shadow' 'expat' 'js')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection')
replaces=('policykit')
provides=("polkit=$pkgver")
conflicts=("polkit")
options=('!libtool')
install=polkit.install
source=(http://www.freedesktop.org/software/polkit/releases/$_axepkgname-$pkgver.tar.gz
        polkit.pam fix-empty-wheel.patch)
md5sums=('0e4f9c53f43fd1b25ac3f0d2e09b2ae1'
         '6564f95878297b954f0572bc1610dd15'
         'c99ab2a7919ad0b69fde4804c043b07f')

build() {
  cd $_axepkgname-$pkgver

  # https://bugs.archlinux.org/task/31717
  patch -Np1 -i ../fix-empty-wheel.patch

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/polkit-1 \
      --disable-static \
      --with-authfw=shadow \
      --enable-gtk-doc
  make
}

package() {
  cd $_axepkgname-$pkgver
  make DESTDIR="$pkgdir" install

  chown 102 "$pkgdir/etc/polkit-1/rules.d"
  chown 102 "$pkgdir/usr/share/polkit-1/rules.d"

  install -Dm644 "$srcdir/polkit.pam" "$pkgdir/etc/pam.d/polkit-1"
}
#----------------axe warning------------------ syetemd detected in file /root/axebuilder/AxePkgbuildsUpdatable/polkit/PKGBUILD 
