# $Id$
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Contributor: Ivailo Monev <xakepa10@gmail.com>
#--progress needs work

#--------------------
pkgbase='lvm2'
pkgname=('lvm2' 'device-mapper')
#--------------------
pkgver=2.02.98
_arch_rel=3
_axe_rel=1
#--------------------
pkgrel="${_AXE_PREFIX_REL}.${_AXE_STAGE}.${_arch_rel}.${_axe_rel}.${_AXE_PROCESOR_REL}.${_AXE_SUFFIX_REL}"
arch=('i686' 'x86_64')
url="http://sourceware.org/lvm2/"
license=('GPL2' 'LGPL2.1')
source=("ftp://sources.redhat.com/pub/lvm2/LVM2.${pkgver}".tgz{,.asc}
        'lvm2_install'
        'lvm2_hook'
        '11-dm-initramfs.rules')
sha1sums=('077425115b164c335a9930df745943e9ea666a8a'
          '4713c852fc668e6fbd36ff7c6b5675b10a95a0f8'
          '3e1680f9b76ce9150d08865d99db90fd15532271'
          'cedc9948123c870f9c5aa3357d0075b41a9c8135'
          'f6a554eea9557c3c236df2943bb6e7e723945c41')

build() {
  cd "${srcdir}/LVM2.${pkgver}"
  unset LDFLAGS

  ./configure --prefix=/ --sbindir=/sbin --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share \
    --includedir=/usr/include --with-usrlibdir=/usr/lib  --libdir=/usr/lib --with-udev-prefix=/usr \
    --enable-pkgconfig \
	--enable-readline \
    --enable-dmeventd \
	--enable-cmdlib \
	--enable-applib \
	--enable-udev_sync \
	--enable-udev_rules  \
    --with-default-locking-dir=/run/lock/lvm
  make
}

package_device-mapper() {
  pkgdesc="Device mapper userspace library and tools  --maintained as part of the axe project"
  url="http://sourceware.org/dm/"
  depends=('glibc' 'udev')
  groups=('axis' 'base')

  cd "${srcdir}/LVM2.${pkgver}"
  make DESTDIR="${pkgdir}" install_device-mapper
  # extra udev rule for device-mapper in initramfs
  install -D -m644 "${srcdir}/11-dm-initramfs.rules" "${pkgdir}/usr/lib/initcpio/udev/11-dm-initramfs.rules"
}

package_lvm2() {
  pkgdesc="Logical Volume Manager 2 utilities  --maintained as part of the axe project"
  depends=('bash' "device-mapper>=${pkgver}" 'udev' 'readline')
  backup=('etc/lvm/lvm.conf')
  options=('!makeflags')
  groups=('axis' 'base')

  cd "${srcdir}/LVM2.${pkgver}"
  make DESTDIR="${pkgdir}" install_lvm2
  # install applib
  make -C liblvm DESTDIR="${pkgdir}" install
  
  # /etc directories
  install -d "${pkgdir}/etc/lvm/"{archive,backup}
  
  # mkinitcpio hook
  install -D -m644 "${srcdir}/lvm2_hook" "${pkgdir}/usr/lib/initcpio/hooks/lvm2"
  install -D -m644 "${srcdir}/lvm2_install" "${pkgdir}/usr/lib/initcpio/install/lvm2"
  
  # systemd support
  # install -D -m644 "${srcdir}/lvm.service" "${pkgdir}/usr/lib/systemd/system/lvm.service"
  # install -D -m644 "${srcdir}/lvm-on-crypt.service" "${pkgdir}/usr/lib/systemd/system/lvm-on-crypt.service"
   install -D -m644 "${srcdir}/lvm2.conf" "${pkgdir}/usr/lib/tmpfiles.d/lvm2.conf"
}