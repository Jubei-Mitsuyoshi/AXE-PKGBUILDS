# $Id: PKGBUILD 173666 2012-12-20 14:18:03Z stephane $
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
#commit 	05a62636aa53c026565019438d87805538e23576 1.10.3-1 > 1.11-1 ----- upstream update ------ accepted by axe, axe rel remains 1



#--------------------
pkgname=krb5
#--------------------
pkgver=1.11
_arch_rel=1
_axe_rel=1
#--------------------
pkgrel="${_AXE_PREFIX_REL}.${_AXE_STAGE}.${_arch_rel}.${_axe_rel}.${_AXE_PROCESOR_REL}.${_AXE_SUFFIX_REL}"
pkgdesc="The Kerberos network authentication system"
arch=('i686' 'x86_64')
url="http://web.mit.edu/kerberos/"
license=('custom')
groups=("axis")
depends=('e2fsprogs' 'libldap' 'keyutils')
makedepends=('perl')
backup=('etc/krb5.conf' 'var/lib/krb5kdc/kdc.conf')
source=(http://web.mit.edu/kerberos/dist/${pkgname}/1.11/${pkgname}-${pkgver}-signed.tar
        krb5-kadmind
        krb5-kadmind.service
        krb5-kdc
        krb5-kdc.service
        krb5-kpropd
        krb5-kpropd.service
        krb5-kpropd@.service
        krb5-kpropd.socket)
sha1sums=('dcdc305bd2b63cf8b8cb698cb9ea28f0a6282136'
          'c4baedcfa35de9ea1031cece1642379da31c0c09'
          'a2a01e7077d9e89cda3457ea0e216debb3dc353c'
          '1568f04dee7fee95002572c1841edcb81b0f8c7e'
          'f5e4fa073e11b0fcb4e3098a5d58a4f791ec841e'
          'b8f8ce630e8bbcb958996a152f39ce029b84bd40'
          '614401dd4ac18e310153240bb26eb32ff1e8cf5b'
          '023a8164f8ee7066ac814486a68bc605e79f6101'
          'f3677d30dbbd7106c581379c2c6ebb1bf7738912')
options=('!emptydirs')

build() {
   tar zxvf ${pkgname}-${pkgver}.tar.gz
   cd "${srcdir}/${pkgname}-${pkgver}/src"

   rm lib/krb5/krb/deltat.c

   # FS#25384
   sed -i "/KRB5ROOT=/s/\/local//" util/ac_check_krb5.m4

   export CFLAGS+=" -fPIC -fno-strict-aliasing -fstack-protector-all"
   export CPPFLAGS+=" -I/usr/include/et"
   ./configure --prefix=/usr \
               --mandir=/usr/share/man \
               --localstatedir=/var/lib \
               --enable-shared \
               --with-system-et \
               --with-system-ss \
               --disable-rpath \
               --without-tcl \
               --enable-dns-for-realm \
               --with-ldap \
               --without-system-verto
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}/src"
   make DESTDIR="${pkgdir}" EXAMPLEDIR=/usr/share/doc/${pkgname}/examples install

   # Fix FS#29889
   install -m 644 plugins/kdb/ldap/libkdb_ldap/kerberos.{ldif,schema} "${pkgdir}"/usr/share/doc/${pkgname}/examples

   # Sample KDC config file
   install -dm 755 "${pkgdir}"/var/lib/krb5kdc
   install -pm 644 config-files/kdc.conf "${pkgdir}"/var/lib/krb5kdc/kdc.conf

   # Default configuration file
   install -dm 755 "${pkgdir}"/etc
   install -pm 644 config-files/krb5.conf "${pkgdir}"/etc/krb5.conf

   install -dm 755 "${pkgdir}"/etc/rc.d
   install -m 755 ../../krb5-{kdc,kadmind,kpropd} "${pkgdir}"/etc/rc.d

   install -dm 755 "${pkgdir}"/usr/share/aclocal
   install -m 644 util/ac_check_krb5.m4 "${pkgdir}"/usr/share/aclocal

   install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/NOTICE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

   # systemd stuff
#   install -dm 755 "${pkgdir}"/usr/lib/systemd/system
#   install -m 644 ../../krb5-{kadmind.service,kdc.service,kpropd.service,kpropd@.service,kpropd.socket} \
 #     "${pkgdir}"/usr/lib/systemd/system
}
#----------------axe warning------------------ syetemd detected in file /root/axebuilder/AxePkgbuildsUpdatable/krb5/PKGBUILD 
#----------------axe warning------------------ /root/axebuilder/AxePkgbuildsUpdatable/krb5/krb5-kadmind altered for rc.d changes
#----------------axe warning------------------ /root/axebuilder/AxePkgbuildsUpdatable/krb5/krb5-kdc altered for rc.d changes
#----------------axe warning------------------ /root/axebuilder/AxePkgbuildsUpdatable/krb5/krb5-kpropd altered for rc.d changes

#axe hand notes, this only installs sysd services, can be given back if get pacman to block extraction etc