# Maintainer: Max Roder <maxroder@web.de>
# Contributor: Lex Rivera <x-demon@x-demon.org>

pkgname=airoscript-ng
pkgver=1.2rc3
pkgrel=4
pkgdesc="A gui mainly for aircrack-ng. Automates cracking of WEP and WPA"
arch=('any')
url="http://code.google.com/p/airoscript/"
license=('GPL2')
depends=('aircrack-ng' 'macchanger')
optdepends=('mdk3: deeper penetration testing')
options=('!makeflags')
source=(http://airoscript.googlecode.com/files/${pkgname}-1.2-3.tar.gz)
sha256sums=('2283e333a0de02c1403815b34ad1526b8ada7f505d7ce31dd3495ebd8300c67d')

package() {
  cd $srcdir/${pkgname}-1.2

  # Remove broken install parts
  sed -i '/install-docs \\/ d' Makefile-Linux
  sed -i '/install-locale \\/ d' Makefile-Linux
  sed -i '/install-desktop \\/ d' Makefile-Linux

  # Finally make...
  make DESTDIR=${pkgdir} \
	prefix=usr \
	picdir=${pkgdir}/usr/share/pixmaps/ \
	appdir=${pkgdir}/usr/share/applications/ \
	etcdir=${pkgdir}/etc/${pkgname}  || return 1

  # Patch config - replace "usr/local" by "usr"
  sed -i 's/usr\/local/usr/g' ${pkgdir}/etc/${pkgname}/airoscript-ng_advanced.conf
  
  # Patch executable to find config files
  sed -i 's/etc\//etc\/airoscript-ng\//g' ${pkgdir}/usr/sbin/${pkgname}

  # Clean up
  rm -rf ${pkgdir}/usr/share/locale \
	${pkgdir}/usr/share/doc \
	${pkgdir}/usr/share/man
}

# vim:set ts=2 sw=2 et: