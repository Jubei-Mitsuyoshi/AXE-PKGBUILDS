# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
pkgname='dhcpcd'
pkgdesc="RFC2131 compliant DHCP client daemon,  --maintained as part of the axe project"
pkgver=5.6.3
_arch_rel=2
_axe_rel=1

pkgrel="${_AXE_EXPORTED_PREFIX_REL}.${_AXE_EXPORTED_STAGE}.${_arch_rel}.${_axe_rel}.${_AXE_EXPORTED_PROCESSOR_TYPE}.${_AXE_EXPORTED_SUFFIX_REL}"
groups=('axis' 'base')
url='http://roy.marples.name/projects/dhcpcd'
arch=('i686' 'x86_64')
license=('BSD')
depends=('glibc' 'sh')
backup=('etc/conf.d/dhcpcd' 'etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/${pkgname} Directory
source=("http://roy.marples.name/downloads/${pkgname}/${pkgname}-$pkgver.tar.bz2" \
        'dhcpcd.conf.d' 
		'dhcpcd.rc')
sha1sums=('d64b589d15821aa5851fda81078183cc61a3e45a'
          'B67B9CE6A2FAACA75FEA356966A16BE2283B7DB0'
          'DB9669E9652E77BE7D9E3C18DB83D255775A9387')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # configure variables
  ./configure \
    --libexecdir=/usr/lib/${pkgname} \
    --dbdir=/var/lib/${pkgname} \
	--rundir=/run

  # Build
  make
}

package() {
  cd "${srcdir}/dhcpcd-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Create Binary Symlink
  install -d "${pkgdir}/usr/sbin"
  ln -sf "/sbin/dhcpcd" "${pkgdir}/usr/sbin/dhcpcd"

  # Install Configuration File used in /etc/rc.d/network
  install -D -m644 "${srcdir}/${pkgname}.conf.d" "${pkgdir}/etc/conf.d/dhcpcd"

  # Install License
  install -d "${pkgdir}/usr/share/licenses/dhcpcd"
  awk '{if(FNR<27)print $0}' "${srcdir}/dhcpcd-${pkgver}/configure.h" \
        >> "${pkgdir}/usr/share/licenses/dhcpcd/LICENSE"

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> "${pkgdir}/etc/dhcpcd.conf" # Disable ip4vall

  # install rc daemon script
  install -Dm755 "${srcdir}/dhcpcd.rc" "${pkgdir}/etc/rc.d/dhcpcd"
}
