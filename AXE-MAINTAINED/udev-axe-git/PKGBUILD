# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
pkgname='udev-axe-git'
pkgdesc="The userspace dev tools (udev), --maintained as part of the axe project"
pkgver=20121027
_arch_rel=0
_axe_rel=1
_axe_proc="xx"
_axe_bin=1
pkgrel=999.${_axe_bin}.${_arch_rel}.${_axe_rel}.${_axe_proc}.axe
arch=('i686' 'x86_64')
url="https://bitbucket.org/braindamaged/udev"
license=('GPL')
groups=('axis' 'base')
provides=('libsystemd' 'systemd-tools' "udev=$pkgver")
conflicts=('udev-axe' 'systemd')
depends=('util-linux' 'glib2' 'kmod' 'hwids' 'bash' 'acl')
makedepends=('git' 'gobject-introspection' 'gperf' 'libxslt' 'docbook-xsl')
options=(!makeflags !libtool)
install="${pkgname}.install"
backup=('etc/udev/udev.conf')
source=('initcpio_hooks'
        'initcpio_install')
options=(!makeflags !libtool)
md5sums=('782ec5acdf48d2e8c06724225fe00623'
         '5dc65936221938659aa53f389eaa3d7d')
_gitroot=('https://bitbucket.org/braindamaged/udev.git')
_gitname=('udev')
         
build() {
  if [ -d "${_gitname}" ] ; then
    cd "${srcdir}/${_gitname}"
    msg2 "Updating local tree..."
    git pull origin master
  else
    cd "${srcdir}"
    msg2 "Cloning initial copy of ${_gitname}..."
    git clone "${_gitroot}" "${_gitname}"
    cd "${srcdir}/${_gitname}"
  fi

  msg2 "Cloning repository for build..."
  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  msg2 "Configuring sources..."
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --bindir=/usr/sbin \
              --libexecdir=/usr/lib \
              --with-firmware-path=/usr/lib/firmware/updates:/lib/firmware/updates:/usr/lib/firmware:/lib/firmware \
              --with-usb-ids-path=/usr/share/hwdata/usb.ids \
              --with-pci-ids-path=/usr/share/hwdata/pci.ids \
              --enable-logging \
              --enable-gudev \
              --enable-introspection \
              --enable-rule_generator

  msg2 "Compiling..."
  make
}


package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install

  # install the mkinitpcio hook
  install -D -m644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
  install -D -m644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"

  # fix wrong path to /bin/sh
  sed -i -e 's#/usr/bin/sh#/bin/sh#g' "${pkgdir}/usr/lib/udev/keyboard-force-release.sh"

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  for i in "${pkgdir}/usr/lib/udev/rules.d/"*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
  done
}
