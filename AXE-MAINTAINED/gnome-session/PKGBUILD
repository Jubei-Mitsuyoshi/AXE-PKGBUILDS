# $Id$
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>



#--------------------
pkgname=gnome-session
#--------------------
pkgver=3.6.2
_arch_rel=1
_axe_rel=1
#--------------------
pkgrel="${_AXE_PREFIX_REL}.${_AXE_STAGE}.${_arch_rel}.${_axe_rel}.${_AXE_PROCESOR_REL}.${_AXE_SUFFIX_REL}"
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('consolekit' 'dconf' 'gconf' 'gsettings-desktop-schemas' 'gtk3'
         'hicolor-icon-theme' 'json-glib' 'libgl' 'libsm' 'libxtst' 'polkit-gnome'
         'upower')
makedepends=('intltool' 'mesa' 'xtrans')
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
groups=('gnome' 'axis')
source=(http://download.gnome.org/sources/$pkgname/${pkgver::3}/$pkgname-$pkgver.tar.xz
        gnome-session-3.3.1-llvmpipe.patch timeout.patch)
sha256sums=('2e3a3466eed4136826e150b639c63beb5c93710346dea2e538adfe5e18d701b4'
            '59ea58012febe48a67dabd944dc638400637ad4d296d63b63b585f10eea4f85e'
            '9eaf31857b41db417475c3b14adc11b10c8226ed76978cdf96dd648fa6e505fc')

build() {
  cd $pkgname-$pkgver

  # Allow software rendering
  patch -Np1 -i ../gnome-session-3.3.1-llvmpipe.patch

  # Increase timeout, for slow machines
  patch -Np1 -i ../timeout.patch

  ./configure --prefix=/usr \
			--sysconfdir=/etc \
			--localstatedir=/var \
			--libexecdir=/usr/lib/gnome-session \
			--disable-schemas-compile \
			--disable-deprecation-flags \
			--disable-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
