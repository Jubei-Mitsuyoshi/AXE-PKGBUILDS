# Maintainer: Ner0
# Contributor: Dave Reisner <d@falconindy.com>
# Contributor: Keshav P R <skodabenz at rocketmail dot com>

pkgname=lxappearance2-git
pkgver=20120514
pkgrel=1
pkgdesc="GTK+ theme switcher for LXDE (manages icons and fonts) - Version 2 - GIT"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://lxde.org/"
depends=('gtk2' 'intltool')
makedepends=('git' 'pkgconfig' 'gettext' 'docbook-xsl')
conflicts=('lxappearance')
provides=('lxappearance')

_gitroot=git://lxde.git.sourceforge.net/gitroot/lxde/lxappearance
_gitname=lxappearance

build() {
  msg "Connecting to GIT server...."

  if [[ -d "$srcdir/$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i 's/1\.11/1\.12/g' ./autogen.sh
  sed -i 's/AC_PROG_CC/AC_PROG_CC\nAM_PROG_CC_C_O/' ./configure.ac

  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --enable-man
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}
