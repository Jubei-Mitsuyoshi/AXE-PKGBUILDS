# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
pkgname='cronie'
pkgdesc="Daemon that runs specified programs at scheduled times and related tools, --maintained as part of the axe project"
pkgver=1.4.8
pkgrel=99.3axe
url='https://fedorahosted.org/cronie/'
license=('custom:BSD')
groups=('axis' 'base')
arch=('i686' 'x86_64')
depends=('bash' 'run-parts')
optdepends=('pm-utils: defer anacron when on battery power'
            'smtp-server: sending cron job output via email')

source=("https://fedorahosted.org/releases/c/r/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'cron.deny'
        'cronie.rc')
sha1sums=('1d2ce3a6ca2a6f96ff31921e4060be3199dc10f3'
          '0f279b8fb820340267d578dc85511c980715f91e'
          'c08c040ed5cb12bc4fd15639a5242d31ec247ef5')

backup=('etc/anacrontab'
        'etc/conf.d/crond'
        'etc/cron.deny')

conflicts=('cron')
provides=('cron')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-anacron \
		--with-inotify

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	chmod u+s "${pkgdir}"/usr/bin/crontab
	install -d "${pkgdir}"/var/spool/{ana,}cron
	install -d "${pkgdir}"/etc/cron.{d,hourly,daily,weekly,monthly}

	install -Dm755 "${srcdir}/cronie.rc" "${pkgdir}/etc/rc.d/crond"
    install -Dm644 "${srcdir}/cron.deny" "${pkgdir}/etc/cron.deny"

	install -Dm644 crond.sysconfig "${pkgdir}/etc/conf.d/crond"
	install -Dm644 contrib/anacrontab "${pkgdir}/etc/anacrontab"
	install -Dm644 contrib/0hourly "${pkgdir}/etc/cron.d/0hourly"
	install -Dm755 contrib/0anacron "${pkgdir}/etc/cron.hourly/0anacron"

	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/cronie/COPYING
}
