# Maintainer: megadriver <megadriver at gmx dot com>
# Based on xfce4-session from [extra]

pkgname=xfce4-session-light
_pkgname=xfce4-session
pkgver=4.10.0
pkgrel=3
pkgdesc="A session manager for Xfce (GNOME and *kit free!)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
depends=('libxfce4ui' 'libwnck' 'libsm' 'xorg-iceauth' 'xorg-xinit' 'hicolor-icon-theme')
makedepends=('intltool')
replaces=('xfce-utils')
conflicts=('xfce4-session' 'upower' 'polkit' 'consolekit')
provides=('xfce4-session')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$_pkgname/4.10/$_pkgname-$pkgver.tar.bz2)
md5sums=('4768e1a41a0287af6aad18b329a0f230')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
