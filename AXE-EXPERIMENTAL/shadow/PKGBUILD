# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Ivailo Monev <xakepa10@gmail.com>
pkgname='shadow'
pkgdesc="Password and account management tool suite with support for shadow files, --maintained as part of the axe project"
pkgver=4.1.5.1
pkgrel=99.1axe
arch=('i686' 'x86_64')
url='http://pkg-shadow.alioth.debian.org/'
license=('BSD')
groups=('base' 'axe')
depends=('bash' 'acl' 'cracklib')
backup=(etc/login.defs
        etc/default/useradd)
options=('!libtool')
install='shadow.install'
source=("http://pkg-shadow.alioth.debian.org/releases/$pkgname-$pkgver.tar.bz2"
        LICENSE
        adduser
        login.defs
        shadow.cron.daily
        useradd.defaults
        xstrdup.patch
        shadow-strncpy-usage.patch)
sha1sums=('81f38720b953ef9c2c100c43d02dfe19cafd6c30'
          '33a6cf1e44a1410e5c9726c89e5de68b78f5f922'
          '78ec184a499f9708adcfcf0b7a3b22a60bf39f91'
          'e5cab2118ecb1e61874cde842d7d04d1003f35cb'
          '5d83ba7e11c765c951867cbe00b0ae7ff57148fa'
          '9ae93de5987dd0ae428f0cc1a5a5a5cd53583f19'
          '6010fffeed1fc6673ad9875492e1193b1a847b53'
          '21e12966a6befb25ec123b403cd9b5c492fe5b16')

build() {
  cd "$pkgname-$pkgver"

  # avoid transitive linking issues with binutils 2.22
  sed -i '/^user\(mod\|add\)_LDADD/s|$| -lattr|' src/Makefile.am

  # link to glibc's crypt(3)
  LDFLAGS+=" -lcrypt"

  # need to offer these upstream
  patch -Np1 <"$srcdir/xstrdup.patch"
  patch -Np1 <"$srcdir/shadow-strncpy-usage.patch"

  ./configure \
    --prefix=/usr \
    --libdir=/lib \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --with-libcrack \
    --without-libpam \
    --without-selinux

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/shadow/LICENSE"

  # interactive useradd
  install -Dm755 "$srcdir/adduser" "$pkgdir/usr/sbin/adduser"

  # useradd defaults
  install -Dm644 "$srcdir/useradd.defaults" "$pkgdir/etc/default/useradd"

  # cron job
  install -Dm744 "$srcdir/shadow.cron.daily" "$pkgdir/etc/cron.daily/shadow"

  # login.defs
  install -Dm644 "$srcdir/login.defs" "$pkgdir/etc/login.defs"
  
   # Remove utilities provided by util-linux
  rm \
      "$pkgdir"/usr/bin/{chsh,chfn,sg} \
      "$pkgdir"/bin/{login,su} \
      "$pkgdir"/usr/sbin/{vipw,vigr}
  

  # but we keep newgrp, as sg is really an alias to it
  mv "$pkgdir"/usr/bin/{newgrp,sg}

  # ...and their many man pages
  find "$pkgdir"/usr/share/man \
      '(' -name 'chsh.1'  -o \
          -name 'chfn.1'  -o \
          -name 'su.1'    -o \
          -name 'login.1' -o \
          -name 'vipw.8'  -o \
          -name 'vigr.8'  -o \
          -name 'newgrp.1' ')' \
      -delete
  rmdir \
      "$pkgdir"/usr/share/man/{fi,id,zh_TW}/man1 \
      "$pkgdir"/usr/share/man/{fi,ko/man8}
}
