# $Id: PKGBUILD 80336 2012-11-20 10:54:00Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
#commit 	---- 1.0.0 ----- initial commit to axe system  ------ accepted by axe, axe rel is 1
#commit 	de3bb57d316af3c37614bbc57a5b629d020b4028 1.0.0-4 > 1.0.1-1 ----- upd  ------ accepted by axe, axe rel remains 1


#--------------------
pkgname=libvirt
#--------------------
pkgver=1.0.1
_arch_rel=1
_axe_rel=1
#--------------------
pkgrel="${_AXE_PREFIX_REL}.${_AXE_STAGE}.${_arch_rel}.${_axe_rel}.${_AXE_PROCESOR_REL}.${_AXE_SUFFIX_REL}"
pkgdesc="API for controlling virtualization engines (openvz,kvm,qemu,virtualbox,xen,etc)"
arch=('i686' 'x86_64')
url="http://libvirt.org/"
license=('LGPL')
groups=("axis")
depends=('e2fsprogs' 'gnutls' 'iptables' 'libxml2' 'parted' 'python2'
	 'avahi' 'yajl' 'libpciaccess' 'udev' 'dbus' 'libxau' 'libxdmcp' 'libpcap'
	 'curl' 'libsasl' 'libgcrypt' 'libgpg-error' 'openssl' 'libxcb' 'gcc-libs'
	 'iproute2' 'netcf' 'libnl' 'libx11' 'gettext')
makedepends=('pkgconfig' 'lvm2' 'linux-api-headers')
optdepends=('bridge-utils: for briged networking (default)'
	    'dnsmasq: for NAT/DHCP for guests'
	    'kvm'
	    'openbsd-netcat: for remote management over ssh'
	    'qemu'
	    'radvd'
	    'dmidecode')
options=('emptydirs' '!libtool')
backup=('etc/conf.d/libvirtd'
	'etc/conf.d/libvirtd-guests'
	'etc/libvirt/libvirtd.conf'
	'etc/libvirt/libvirt.conf'
	'etc/libvirt/qemu.conf'
	'etc/sasl2/libvirt.conf')
install="libvirt.install"
source=("http://libvirt.org/sources/$pkgname-$pkgver.tar.gz"
	libvirtd.rc.d
	libvirtd.conf.d
	libvirtd-guests.rc.d
	libvirtd-guests.conf.d
	libvirt.tmpfiles.d
	openbsd-netcat-default.patch)
md5sums=('86a8c0acabb01e11ac84fe00624dc54e'
         'c43244c40a0437038c82089618e7beaa'
         '3ed0e24f5b5e25bf553f5427d64915e6'
         '0ee5b6c58590ff392a266f20f7928d1f'
         '0a96ed876ffb1fcb9dff5a9b3a609c1e'
         '8d98e62915785686b0b6c8c070628392'
         '456723b41903d3aaa7ec948c1feea265')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  export PYTHON=`which python2`
  for file in $(find . -name '*.py' -print); do
    sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

#  patch -Np1 -i "$srcdir"/openbsd-netcat-default.patch

  export LDFLAGS=-lX11
  export RADVD=/usr/sbin/radvd
  [ -f Makefile ] || ./configure --prefix=/usr \
								--libexec=/usr/lib/"$pkgname" \
								--with-storage-lvm \
								--without-xen \
								--with-udev \
								--without-hal \
								--without-polkit \
								--without-sanlock \
								--without-capng \
								--disable-static \
								--disable-nls \
								--with-storage-fs \
								--with-storage-disk \
								--with-storage-mpath \
								--with-numactl \
								--with-network \
								--with-vbox
  make -j1
  sed -i 's|/etc/sysconfig/libvirtd|/etc/conf.d/libvirtd|' daemon/libvirtd.service
  sed -i 's|/etc/sysconfig/libvirt-guests|/etc/conf.d/libvirtd-guests|' tools/libvirt-guests.service
  sed -i 's|/etc/init.d/libvirt-g|/etc/rc.d/libvirtd-g|g' tools/libvirt-guests.service
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -D -m755 "$srcdir"/libvirtd.rc.d "$pkgdir"/etc/rc.d/libvirtd
  install -D -m644 "$srcdir"/libvirtd.conf.d "$pkgdir"/etc/conf.d/libvirtd

  install -D -m755 "$srcdir"/libvirtd-guests.rc.d "$pkgdir"/etc/rc.d/libvirtd-guests
  install -D -m644 "$srcdir"/libvirtd-guests.conf.d "$pkgdir"/etc/conf.d/libvirtd-guests


#  install -dm0755 $pkgdir/usr/lib/sysctl.d
#  mv $pkgdir/etc/sysctl.d/libvirtd $pkgdir/usr/lib/sysctl.d/libvirtd

  # systemd stuff
#  install -D -m644 "$srcdir"/libvirt.tmpfiles.d "$pkgdir"/usr/lib/tmpfiles.d/libvirt.conf
  mv $pkgdir/lib/* $pkgdir/usr/lib/

#  sed -i \
#	's|After=.*|After=syslog.target network.target libvirtd.service|' \
#	$pkgdir/usr/lib/systemd/system/libvirt-guests.service

  rm -rf \
	$pkgdir/var/run \
	$pkgdir/etc/sysconfig \
	$pkgdir/etc/rc.d/init.d \
	$pkgdir/lib \
	$pkgdir/etc/sysctl.d
}


#axe hand notes, will disable polkit on this one seems cool from gentoo ebuild
#needs to be linked to vbox which is the best