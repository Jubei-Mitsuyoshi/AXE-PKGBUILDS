# $Id: PKGBUILD 155725 2012-04-06 00:03:55Z tomegun $
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
pkgname=pcmciautils
pkgver=018
_axe_pkgrel=1
pkgrel=991.${_axe_pkgrel}.4axe
pkgdesc="Utilities for inserting and removing PCMCIA cards"
arch=(i686 x86_64)
url="http://kernel.org/pub/linux/utils/kernel/pcmcia/pcmcia.html"
license=('GPL')
groups=('axis' 'base')
# sysfsutils will not be required in the next release
depends=('udev' 'sysfsutils')
conflicts=('pcmcia-cs')
# source=(http://kernel.org/pub/linux/utils/kernel/pcmcia/${pkgname}-${pkgver}.tar.bz2) broken due to breakin
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz
	initcpio-install-pcmcia)
options=(!makeflags)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's,/usr/bin/install,install,g;
             s,/lib/udev,/usr/lib/udev,g' Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # install the mkinitpcio hook
  install -D -m644 ../initcpio-install-pcmcia ${pkgdir}/usr/lib/initcpio/install/pcmcia
}
md5sums=('964c802994677a71b38ec56554aa229f'
         '2659576a4637c58c4e79a96f8e43e316')
