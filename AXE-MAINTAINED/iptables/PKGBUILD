# $Id: PKGBUILD 170004 2012-10-30 23:08:48Z thomas $
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=iptables
pkgver=1.4.16.2
_arch_rel=1
_axe_rel=1
_axe_proc="xx"
_axe_bin=1
pkgrel=999.${_axe_bin}.${_arch_rel}.${_axe_rel}.${_axe_proc}.axe
pkgdesc='Linux kernel packet control tool --maintained as part of the axe project'
arch=('i686' 'x86_64')
license=('GPL2')
groups=('axis')
url='http://www.netfilter.org/projects/iptables/index.html'
depends=('glibc' 'bash')
makedepends=('linux-api-headers')
options=('!libtool')
source=("http://www.iptables.org/projects/iptables/files/${pkgname}-${pkgver}.tar.bz2"
        iptables
        ip6tables
        empty.rules
        simple_firewall.rules
        iptables.conf.d
        empty-filter.rules
        empty-mangle.rules
        empty-nat.rules
        empty-raw.rules
        empty-security.rules
        0503-extension_cppflags.patch
        iptables.service
        ip6tables.service
        iptables-flush)
backup=(etc/conf.d/iptables)
sha1sums=('6fd368c4f6c31752011a716cccd951601157a493'
          '5bb6fa526665cdd728c26f0f282f5a51f220cf88'
          '2db68906b603e5268736f48c8e251f3a49da1d75'
          '83b3363878e3660ce23b2ad325b53cbd6c796ecf'
          '9907f9e815592837abc7fa3264a401567b7606ab'
          'cdb830137192bbe002c6d01058656bd053ed0ddd'
          'd9f9f06b46b4187648e860afa0552335aafe3ce4'
          'c45b738b5ec4cfb11611b984c21a83b91a2d58f3'
          '1694d79b3e6e9d9d543f6a6e75fed06066c9a6c6'
          '7db53bb882f62f6c677cc8559cff83d8bae2ef73'
          'ebbd1424a1564fd45f455a81c61ce348f0a14c2e'
          '44626980a52e49f345a0b1e1ca03060f3a35763c'
          '5c4eb4ea88c302e8ff98f435a11dd59b00f4d8b9'
          'f1f16f44c6a5547b6f251d13007fe6585761e8b0'
          'e7abda09c61142121b6695928d3b71ccd8fdf73a')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # use system one
  rm include/linux/types.h

  patch -Np1 -i ${srcdir}/0503-extension_cppflags.patch

 ./configure --prefix=/usr \
     --libexecdir=/usr/lib/iptables --sysconfdir=/etc \
     --with-xtlibdir=/usr/lib/iptables \
     --enable-devel --enable-libipq \
     --enable-shared 

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"
  install -D -m755 iptables "${pkgdir}"/etc/rc.d/iptables
  install -D -m755 ip6tables "${pkgdir}"/etc/rc.d/ip6tables
  install -D -m644 empty.rules "${pkgdir}"/etc/iptables/empty.rules
  install -D -m644 simple_firewall.rules "${pkgdir}"/etc/iptables/simple_firewall.rules
  install -D -m644 iptables.conf.d "${pkgdir}"/etc/conf.d/iptables

  mkdir -p "${pkgdir}"/var/lib/iptables
  install -m644 empty-{filter,mangle,nat,raw,security}.rules "${pkgdir}"/var/lib/iptables
#commented out by axe
  # install systemd files
#  install -Dm644 ${srcdir}/iptables.service ${pkgdir}/usr/lib/systemd/system/iptables.service
#  install -Dm644 ${srcdir}/ip6tables.service ${pkgdir}/usr/lib/systemd/system/ip6tables.service
#  install -Dm755 ${srcdir}/iptables-flush ${pkgdir}/usr/lib/systemd/scripts/iptables-flush  
}

